// ======================================================================
// ktlint
// ----------------------------------------------------------------------
// ktlint is a linter for Kotlin. It will check basic code conventions
// such as unused imports, spaces instead of tabs and appropriate new
// lines. We use the default rule set.
//
// See: https://github.com/shyiko/ktlint
// See: https://ktlint.github.io
// ======================================================================

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.36.0"
}

task ktlint(type: JavaExec, group: "ktlint") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

compileKotlin.mustRunAfter ktlint
check.dependsOn ktlint

// Use this task to import the defaul ktlint rule set to IntelliJ IDEA.
// Afterwards you can use IDEA's "Reformat Code" to apply the ktlint rule
// set to your code. Note that you must restart your IDEA after executing
// this task.
task ktlintIdea(type: JavaExec, group: "ktlint") {
    description = "Import rule set to IDEA."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--apply-to-idea", "-y"
}

// Use this task to auto-format all Kotlin files in the current project.
// This might be convenient in case of a huge number of new and unformatted
// Kotlin files. Note that this task will not fix unused imports - for
// whatever reason.
task ktlintFormat(type: JavaExec, group: "ktlint") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
